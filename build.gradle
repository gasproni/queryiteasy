buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9'
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
}

repositories {
    mavenCentral()
}

apply plugin: "info.solidsoft.pitest"
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'findbugs'
apply plugin: 'maven'
apply plugin: 'signing'

sourceSets {
    acceptanceTest {
        java {
            srcDir 'src/acceptanceTest/java'
        }
        resources {
            srcDir 'src/acceptanceTest/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.compileClasspath
        runtimeClasspath += compileClasspath
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

task acceptanceTest(type: Test, dependsOn: test) {
    description = 'Runs the acceptance tests'
    group = 'verification'
    setTestClassesDir(sourceSets.acceptanceTest.output.classesDir)
    setClasspath(sourceSets.acceptanceTest.runtimeClasspath)
    reports.junitXml.destination = buildDir.path +'/acceptance-test-results'
    reports.html.destination = buildDir.path +'/reports/acceptanceTest'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

check {
    dependsOn(tasks.acceptanceTest)
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'queryiteasy'
                packaging 'jar'
                description 'A Java 8 wrapper to make JDBC easier to use'
                url repositoryUrl

                scm {
                    url 'scm:git:https://github.com/gasproni/queryiteasy.git'
                    connection 'scm:git:https://github.com/gasproni/queryiteasy.git'
                    developerConnection 'scm:git:https://github.com/gasproni/queryiteasy.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gasproni'
                        name 'Giovanni Asproni'
                    }
                }
            }
        }
    }
}

findbugs {
    toolVersion = "3.0.1"
    sourceSets = [sourceSets.main]
    ignoreFailures = false
    reportsDir = file("$project.buildDir/findbugsReports")
    effort = "max"
    reportLevel = "high"
}

pitest {
    targetClasses = ['com.asprotunity.queryiteasy.*']  //by default "${project.group}.*"
    mainSourceSets = [sourceSets.main]
    testSourceSets = [sourceSets.test, sourceSets.acceptanceTest]
    threads = 1
    outputFormats = ['XML', 'HTML']
}

subprojects {
    apply plugin: 'java'
    repositories {
        mavenCentral()
    }

    dependencies {
        compile rootProject, 'org.hsqldb:hsqldb:2.3.3', 'org.postgresql:postgresql:9.4.1209.jre7'
    }
}

dependencies {
    testCompile 'org.hamcrest:hamcrest-all:1.3', 'junit:junit:4.12', 'org.mockito:mockito-all:1.10.19'
    acceptanceTestCompile 'org.hsqldb:hsqldb:2.3.3', 'org.postgresql:postgresql:9.4.1209.jre7',
            'mysql:mysql-connector-java:5.1.38', files(file('jdbclibs').listFiles().findAll {it.name.endsWith('.jar')})
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
    module {
        scopes.TEST.plus += [configurations.acceptanceTestCompile]
    }
}